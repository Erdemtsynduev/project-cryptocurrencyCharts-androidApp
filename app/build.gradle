def versionMajor = 0
def versionMinor = 6
def versionPatch = 9
def versionBuild = 0

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.erdemtsynduev.profitcoin"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_ENDPOINT", '"https://pro-api.coinmarketcap.com/"'
        buildConfigField "String", "API_KEY", '"ADD YOUR API KEY"'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            javaCompileOptions.annotationProcessorOptions.arguments['butterknife.debuggable'] = 'false'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.support.appCompat
    implementation deps.support.constraintLayout
    implementation deps.support.design
    implementation deps.support.cardview
    implementation deps.support.recyclerView

    implementation deps.timber
    implementation deps.gson
    implementation deps.picasso
    implementation deps.eventbus
    implementation deps.baseRecyclerViewAdapterHelper
    implementation deps.dialogplus
    implementation deps.paperdb

    implementation deps.room.runtime
    annotationProcessor deps.room.compiler

    implementation deps.retrofit.core
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2adapter

    implementation deps.okhttp.core
    implementation deps.okhttp.logger

    implementation deps.rx.core
    implementation deps.rx.android

    implementation deps.butterknife.core
    annotationProcessor deps.butterknife.compiler

    implementation deps.dagger2.core
    annotationProcessor deps.dagger2.compiler

    implementation deps.moxy.core
    implementation deps.moxy.compat
    annotationProcessor deps.moxy.compiler

    testImplementation deps.junit
    androidTestImplementation deps.testRunner
    androidTestImplementation deps.testRules
    androidTestImplementation(deps.espresso.core) {
        exclude group: 'com.google.code.findbugs'
    }
}